---
title: Absorption models
author: Hechuan Wang & Vijay Ivaturi
date: August 10, 2019
---

```julia
using Pumas, LinearAlgebra, Plots
```

# Introduction

In this tutorial, we will cover the following absorption models

* first order
* zero-order
* first-order + zero-order simultaneously
* Erlang absorption
* transit absorption

Let's start by setting up a population of subjects to work with that receive a single dose regimen

## Generating the population

```julia
sd = DosageRegimen(100, time=0)
choose_covariates() = (Wt = rand(55:80),dose=100)
sd_with_covariates = Population(map(i -> Subject(id=i,evs=sd,cvs=choose_covariates()),1:10))
```
For a more indepth tutorial, please see the tutorial on [generating and simulation populations](https://tutorials.pumas.ai/html/introduction/simulating_populations.html)

For simplicity, we will use a 1-compartment distribution system with an additional absorption compartment as required.
Note that we are not simulating with residual error in these examples, but that can be easily added


## first-order absorption

```julia
foabs = @model begin

  @param begin
    θ ∈ VectorDomain(3)
    Ω ∈ PDiagDomain(3)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V  = θ[2]*(Wt/70)*exp(η[2])
    Ka = θ[3]*exp(η[3])
  end

  @dynamics begin
    Depot'   = -Ka*Depot
    Central' =  Ka*Depot - Central*CL/V
  end

  @derived begin
    conc = Central/V
  end

end

```
and below are the set of parameters for this model

```julia
param = (
  θ = [5,20,1],
  Ω =Diagonal([0.04,0.04,0.04]),
  )
```

We can now use the data and model with parameters to simulate a first-order absorption profile

```julia
obs = simobs(foabs, sd_with_covariates, param, obstimes=0:1:48)
plot(obs, title =  "First-order absorption")
```

## zero-order absorption

```julia
zoabs = @model begin
  @param begin
    θ ∈ VectorDomain(3)
    Ω ∈ PDiagDomain(3)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt

  @pre begin
    CL = θ[1]*(Wt/11)^0.75*exp(η[1])
    V  = θ[2]*(Wt/11)*exp(η[2])
    duration =  (Central = θ[3],)
  end

  @dynamics begin
    Central' =  - Central*CL/V
  end

  @derived begin
    conc = Central/V
  end

end
```

and below are the set of parameters for this model

```julia
param = (
  θ = [0.792, 13.7, 5],
  Ω =Diagonal([0.04,0.04,0.04]),
  )
```

We can now use the data and model with parameters to simulate a zero-order absorption profile

```julia
sd = DosageRegimen(100, time=0, rate=-2)
choose_covariates() = (Wt = rand(55:80),dose=100)
sd_with_covariates = Population(map(i -> Subject(id=i,evs=sd,cvs=choose_covariates()),1:10))
```

Note that while modeling duration, `rate` data item has to be set to `< 0`

```julia
obs = simobs(zoabs, sd_with_covariates, param, obstimes=0:0.1:48)
plot(obs, title =  "Zero-order absorption")
```

## simultaneous zero+first order absorption models

```julia

fozoabs = @model begin

  @param begin
    θ ∈ VectorDomain(6)
    Ω ∈ PDiagDomain(2)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V  = θ[2]*(Wt/70)*exp(η[2])
    Ka = θ[3]
    duration = (Central =θ[4],)
    bioav = (Depot = θ[5], Central = 1-θ[5])
    lags = (Central =θ[6],)
  end

  @dynamics begin
    Depot'   = -Ka*Depot
    Central' =  Ka*Depot - Central*CL/V
  end

  @derived begin
    conc = Central/V
  end

end
```

and below are the set of parameters for this model

```julia
param = (
  θ = [0.84, 0.72, 1, 1, 0.1, 2],
  Ω = Diagonal([0.04, 0.04]),
  )
```


We can now use the data and model with parameters to simulate a simultaneous first- and zero-order absorption profile

```julia
sd_fo = DosageRegimen(100, time=0, cmt=1, rate = 0, evid=1)
sd_zo = DosageRegimen(100, time=0, cmt=2, rate = -2, evid=1)
sd_fo_zo = DosageRegimen(sd_fo,sd_zo)

choose_covariates() = (Wt = rand(55:80),dose=100)
sd_with_covariates = Population(map(i -> Subject(id=i,evs=sd_fo_zo,cvs=choose_covariates()),1:10))
```

```julia
obs = simobs(fozoabs, sd_with_covariates, param, obstimes=0:0.1:8, parallel_type=Pumas.Serial)
plot(obs)
```

## Erlang absorption models with two compartment dsitribution

```julia
erlangabs = @model begin
  @param begin
    θ ∈ VectorDomain(5)
    Ω ∈ PDiagDomain(4)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt dose

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V1  = θ[2]*(Wt/70)*exp(η[2])
    Ktr = θ[3]*exp(η[3])
    Q  = θ[4]*(Wt/70)^0.75*exp(η[4])
    V2  = θ[5]*(Wt/70)
    K  = CL/V1
    K21 = Q/V2
    K12 = Q/V1
    N = 5 # number of compartments
  end

  @dynamics begin
    Central' = Ktr*dose*((Ktr*t)^N/factorial(N))*exp(-Ktr*t) - Central*K + K21*Periph -K12*Central
    Periph' = - K21*Periph + K12*Central
  end

  @derived begin
    conc = Central/V1
  end

end
```

```julia
sd = DosageRegimen(100, time=0)
choose_covariates() = (Wt = rand(55:80),dose=100)
sd_with_covariates = Population(map(i -> Subject(id=i,evs=sd,cvs=choose_covariates()),1:10))
```

```julia
param = (
  θ = [26.3, 75.9, 2, 23.9, 43.1],
  Ω = Diagonal([0.04,0.04,0.04,0.04]),
  σ_prop = 0
  )
```
We can now use the data and model with parameters to simulate a Erlang absorption profile

```julia
obs = simobs(erlangabs, sd_with_covariates, param, obstimes=0:0.1:24)
plot(obs)
```


# 5 compartment model with Transit absorption for single dose

```julia
transabs = @model begin
  @param begin
    θ ∈ VectorDomain(6)
    Ω ∈ PDiagDomain(3)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt dose

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V  = θ[2]*(Wt/70)*exp(η[2])
    BIO = θ[3] # the dose was corrected by bioavailability already
    Ka = θ[4]
    MTT =θ[5]*exp(η[3])
    NN = θ[6]
    K  = CL/V
    Ktr = (NN+1)/MTT
    LNFAC=log(2.5066)+(NN+0.5)*log(NN)-NN
  end

  @dynamics begin
    Absorption' = exp(log(BIO*dose+.00001)+log(Ktr)+NN*log(Ktr*t+0.00001)-Ktr*t-LNFAC)-Ka*Absorption
    Central' = Ka*Absorption-Central*K
  end

  @derived begin
    conc = Central/V
  end

end


```

```julia
param = (
  θ = [3.54, 487, 1, 1.77, 1.03, 8.15],
  Ω = Diagonal([0.0361,0.16,0.27])
  )
```
We can now use the data and model with parameters to simulate a transit absorption profile

```julia
obs = simobs(transabs, sd_with_covariates, param, obstimes=0:0.1:120)
plot(obs)
```

## Two parallel first-order absorptions


```julia
two_parallel_foabs = @model begin

  @param begin
    θ ∈ VectorDomain(6)
    Ω ∈ PDiagDomain(6)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V  = θ[2]*(Wt/70)*exp(η[2])
    Ka1 = θ[3]*exp(η[3])
    Ka2 = θ[4]*exp(η[4])
    lags = (Depot2 = θ[5]*exp(η[5]),)
    bioav = (Depot1 = θ[6]*exp(η[6]), Depot2 = 1-θ[6]*exp(η[6]))
  end

  @dynamics begin
    Depot1'   = -Ka1*Depot1
    Depot2'   = -Ka2*Depot2
    Central' =  Ka1*Depot1 + Ka2*Depot2 - Central*CL/V
  end

  @derived begin
    conc = Central/V
  end

end

```
and below are the set of parameters for this model

```julia
param = (
  θ = [5,50,0.8,0.6,5,0.5],
  Ω =Diagonal([0.04,0.04,0.36,0.36,0.04,0.04]),
  )
```

We can now use the data and model with parameters to simulate a first-order absorption profile

```julia
fo_1 = DosageRegimen(100,cmt=1,time=0)
fo_2 = DosageRegimen(100,cmt=2,time=0)
parallel_fo = DosageRegimen(fo_1,fo_2)
parallel_fo_with_covariates = Population(map(i -> Subject(id=i,evs=parallel_fo,cvs=choose_covariates()),1:10))
obs = simobs(two_parallel_foabs, parallel_fo_with_covariates, param, obstimes=0:1:48)
plot(obs, title =  "Two Parallel First-order absorption")
```

## Weibull-Type Absorption

```julia
wb_abs = @model begin

  @param begin
    θ ∈ VectorDomain(4)
    Ω ∈ PDiagDomain(4)
  end

  @random begin
    η ~ MvNormal(Ω)
  end

  @covariates Wt

  @pre begin
    CL = θ[1]*(Wt/70)^0.75*exp(η[1])
    V  = θ[2]*(Wt/70)*exp(η[2])
    Ka1 = θ[3]*exp(η[3])
    Gama1 = θ[4]*exp(η[4])
    # Weibull function
    wb = 1-exp((-Ka1*t)^Gama1)
  end

  @dynamics begin
    Depot'   = -wb*Depot
    Central' =  wb*Depot - Central*CL/V
  end

  @derived begin
    conc = Central/V
  end

end

```
and below are the set of parameters for this model

```julia
param = (
  θ = [5,50,0.4,4],
  Ω =Diagonal([0.04,0.04,0.36,0.04]),
  )
```

We can now use the data and model with parameters to simulate a weibull absorption profile

```julia
wb_reg = DosageRegimen(100,cmt=1,time=0)
wb_reg_with_covariates = Population(map(i -> Subject(id=i,evs=parallel_fo,cvs=choose_covariates()),1:10))
obs = simobs(wb_abs,wb_reg_with_covariates, param, obstimes=0:1:48)
plot(obs, title =  "Two Parallel First-order absorption")
```
